# Instruções

Você precisará usar recursos mais complexos da especificação. Antes de iniciar, cheque como usar
[count](https://www.terraform.io/docs/configuration/interpolation.html) 
e os recursos [VPC padrão](https://www.terraform.io/docs/providers/aws/r/default_vpc.html), 
[subredes](https://www.terraform.io/docs/providers/aws/d/subnet_ids.html), 
[balanceador](https://www.terraform.io/docs/providers/aws/r/lb.html) e como atachar instâncias nele: 
[aws_lb_target_group](https://www.terraform.io/docs/providers/aws/r/lb_target_group.html), 
[aws_lb_target_group_attachment](https://www.terraform.io/docs/providers/aws/r/lb_target_group_attachment.html) 
e 
[aws_lb_listener](https://www.terraform.io/docs/providers/aws/r/lb_listener.html). 
Navegue por esses elementos na documentação e verifique como usá-los. 

1. Altere sua definição de instância para incluir agora 3 servidores. Você pode (e deve) utilizar o recurso
count. Veja o exemplo abaixo:

```
resource "aws_instance" "web-server" {
  ami = "${var.ami}"
  instance_type = "${var.instance}"


  count = 5

  tags {
    Name = "web-server-${count.index}"
   }

   vpc_security_group_ids = ["${aws_security_group.sec-group.id}"]
}
```

Nesse exemplo 5 servidores são criados com o nome web-server-[0-4].



2. Crie seu balanceador com o recurso aws_alb:


```
resource "aws_alb" "alb" {
  name               = "test-lb-tf"
  internal           = false
  load_balancer_type = "application"
  security_groups    = ["${aws_security_group.elb-group.id}"]
  subnets            = ["${data.aws_subnet_ids.all-nets.ids}"]
}
```

3. O balanceador precisa ter acesso externo, portanto precisamos configurar um grupo de segurança. Sigam o modelo
anterior para liberar a porta 80. Um detalhe importante: você também precisará liberar a saída de pacotes do
balanceador. Use um bloco egress. veja o exemplo abaixo:

```
resource "aws_security_group" "elb-group" {
  name = "elb-group"

  egress {
    from_port = 0
    to_port = 0
    protocol = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
  ...
}
```



4. Outro item obrigatório para o balanceador é a identificação das subredes que ele pode trabalhar. Use o 
datasource aws_subnet_ids para listar todos os identificadores e relacioná-los no balanceador. Ficará assim:


```
*** main.tf ***

resource "aws_default_vpc" "default" {
    tags {
        Name = "Default VPC"
    }
}

data "aws_subnet_ids" "all-nets" {
  vpc_id = "${aws_default_vpc.default.id}"
}

resource "aws_alb" "alb" {
  name               = "test-lb-tf"
  internal           = false
  load_balancer_type = "application"
  security_groups    = ["${aws_security_group.elb-group.id}"]
  subnets            = ["${data.aws_subnet_ids.all-nets.ids}"]
}
```

5. Crie um target group e informe seus dados obrigátórios

```
*** main.tf ***

resource "aws_lb_target_group" "tg" {
  name     = "tf-example-lb-tg"
  port     = 8080
  protocol = "HTTP"
  vpc_id   = "${aws_default_vpc.default.id}"
}
```

6. Crie um listener para o balanceador e indique que ação ele deve fazer quando as requisições chegarem

```
*** main.tf ***

resource "aws_lb_listener" "my-listener" {
  load_balancer_arn = "${aws_alb.alb.arn}"
  port              = "80"
  protocol          = "HTTP"

  default_action {
    target_group_arn = "${aws_lb_target_group.tg.arn}"
    type             = "forward"
  }
}
```

7. Por fim, adicione as instância no target group


```
*** main.tf ***

resource "aws_lb_target_group_attachment" "test" {
  count = 3
  target_group_arn = "${aws_lb_target_group.tg.arn}"
  target_id = "${element(aws_instance.web-server.*.id, count.index)}"
  port = "${var.port}"
}
```

8. Para realizarmos um teste, configuramos um servidor http simples utilizando python. Copie o trecho abaixo
na definição das instâncias

```
*** main.tf

resource "aws_instance" "web-server" {
	...
    user_data = <<-EOF
              #!/bin/bash
              cd /home/ec2-user
              touch "INSTANCE-${count.index}"
              sudo pip install http
              nohup python -m SimpleHTTPServer "${var.port}" &
              EOF
  ...
}
```

9. Planeje e aplique as configurações.


10. Lembre-se de destruir sua infra